\setKVdefault[Rapporteurs]{%
   CoinBG={(0,0)},
   CoinHD={(9.5u,4.5u)},
   couleur=Coral,
   angleRapporteur=90,
   angleEnonce=0,
   xencart=0,
   yencart=0,
   xenonce=0,
   yenonce=0,
   interieur=true,
   rapporteurAleph=false,
   Total=false,% pour graduer de 10en 10
   CentreCroix=true,%Pour avoir une croix au centre du rapporteur
}%

\NewDocumentCommand\Rapporteurs{o}{%
  \useKVdefault[Rapporteurs]% On revient aux valeurs par défaut, équivalent à restoreKV[Reseau]
  \setKV[Rapporteurs]{#1}% On lit les arguments optionnels
  \xdef\RapporteursCoinBG{\useKV[Rapporteurs]{CoinBG}}%
  \xdef\RapporteursCoinHD{\useKV[Rapporteurs]{CoinHD}}%
  \xdef\Rapporteurscouleur{\useKV[Rapporteurs]{couleur}}%
  \xdef\RapporteursangleRapporteur{\useKV[Rapporteurs]{angleRapporteur}}%
  \xdef\RapporteursangleEnonce{\useKV[Rapporteurs]{angleEnonce}}%
  \xdef\Rapporteursxencart{\useKV[Rapporteurs]{xencart}}%
  \xdef\Rapporteursyencart{\useKV[Rapporteurs]{yencart}}%
  \xdef\Rapporteursxenonce{\useKV[Rapporteurs]{xenonce}}%
  \xdef\Rapporteursyenonce{\useKV[Rapporteurs]{yenonce}}%
  \xdef\Rapporteursinterieur{\useKV[Rapporteurs]{interieur}}%
  \xdef\RapporteursrapporteurAleph{\useKV[Rapporteurs]{rapporteurAleph}}%
  \xdef\RapporteursTotal{\useKV[Rapporteurs]{Total}}%
  \xdef\RapporteursCentreCroix{\useKV[Rapporteurs]{CentreCroix}}%   
  \begin{Geometrie}[CoinBG=\RapporteursCoinBG,CoinHD=\RapporteursCoinHD]
    trace feuillet;
    input \persopath/commandes/rapporteurs.mp
    pair A[],B,C[];
    %%%% Paramètres
    color couleur;
    numeric angleEnonce,angleRapporteur;
    numeric xencart,yencart,xenonce,yenonce;
    boolean interieur,rapporteurAleph;
    picture enonce,reponse;
    %%%%
    couleur=\Rapporteurscouleur;
    angleRapporteur:=\RapporteursangleRapporteur;
    angleEnonce:=\RapporteursangleEnonce;
    xencart:=\Rapporteursxencart;
    yencart:=\Rapporteursyencart;
    xenonce:=\Rapporteursxenonce;
    yenonce:=\Rapporteursyenonce;
    interieur=\Rapporteursinterieur;
    rapporteurAleph=\RapporteursrapporteurAleph;
    Total:=\RapporteursTotal;% pour graduer de 10en 10
    CentreCroix:=\RapporteursCentreCroix;%Pour avoir une croix au centre du rapporteur
    %%%% Points utiles     
    A0=u*(3.75,0.5);
    B=u*(7.75,0.5);
    if interieur:              
       A1=A0 shifted (u,0);
       A2=rotation(A1,A0,angleRapporteur);
    else:
       A1=A0 shifted (-u,0);
       A2=rotation(A1,A0,-angleRapporteur);
    fi;
    %%%% Rapporteur
    enonce=image(
       draw demidroite(A0,A1) withpen pencircle scaled 1.5bp withcolor couleur;
       draw demidroite(A0,A2) withpen pencircle scaled 1.5bp withcolor couleur;
       if interieur:
          draw marqueangle(A1,A0,A2,0);
          fill coloreangle(A1,A0,A2) withcolor couleur;
       else:
          draw marqueangle(A2,A0,A1,0);
          fill coloreangle(A2,A0,A1) withcolor couleur;
       fi;
       if rapporteurAleph:
          draw rapporteuraleph(A0,B,1);
       else:
          draw myrapporteurdouble(A0,B,1);
       fi;
    );
    %%%% Encart réponse
    reponse=image(
       C0=u*(7,2);
       C1=C0 + (2u,0); 
       C2=C1 + (0,1.5u);
       C3=C2 + (-2u,0);
       C4=iso(C2,C3)-(0,0.5u);
       C5=C4-(0,0.75u);
       draw C0--C1--C2--C3--cycle;
       label(btex Mesure etex,C4);
       label(btex \makebox[0.2\linewidth]{\dotfill} etex,C5);      
    );
    %%%% Tracés
    draw enonce rotatedaround(A0,-angleEnonce) shifted (xenonce*u,yenonce*u);
    draw reponse shifted(xencart*u,yencart*u);     
 \end{Geometrie}
}

