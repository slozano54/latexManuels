\setKVdefault[Rapporteurs]{%
   CoinBG={(0,0)},
   CoinHD={(9.5u,4.5u)},
   couleur=Coral,
   angleRapporteur=90,
   angleEnonce=0,
   decalagexEncart=0,
   decalageyEncart=0,
   decalagexEnonce=0,
   decalageyEnonce=0,
   graduationInterieure=true,
   rapporteurAleph=false,
   graduationTotale=false,% Pour graduer de 10° en 10°
   centreCroix=true,% Pour avoir une croix au centre du rapporteur
   correction=false,% Pour affciher le correction
   construction=false,% Pour un énoncé de type tracer ...
   afficheRapporteur=true,% Pour un énoncé sans rapporteur
}%

\NewDocumentCommand\Rapporteurs{o}{%
  \useKVdefault[Rapporteurs]% On revient aux valeurs par défaut, équivalent à restoreKV[Reseau]
  \setKV[Rapporteurs]{#1}% On lit les arguments optionnels
  \xdef\RapporteursCoinBG{\useKV[Rapporteurs]{CoinBG}}%
  \xdef\RapporteursCoinHD{\useKV[Rapporteurs]{CoinHD}}%
  \xdef\Rapporteurscouleur{\useKV[Rapporteurs]{couleur}}%
  \xdef\RapporteursangleRapporteur{\useKV[Rapporteurs]{angleRapporteur}}%
  \xdef\RapporteursangleEnonce{\useKV[Rapporteurs]{angleEnonce}}%
  \xdef\RapporteursdecalagexEncart{\useKV[Rapporteurs]{decalagexEncart}}%
  \xdef\RapporteursdecalageyEncart{\useKV[Rapporteurs]{decalageyEncart}}%
  \xdef\RapporteursdecalagexEnonce{\useKV[Rapporteurs]{decalagexEnonce}}%
  \xdef\RapporteursdecalageyEnonce{\useKV[Rapporteurs]{decalageyEnonce}}%
  \xdef\RapporteursgraduationInterieure{\useKV[Rapporteurs]{graduationInterieure}}%
  \xdef\RapporteursrapporteurAleph{\useKV[Rapporteurs]{rapporteurAleph}}%
  \xdef\RapporteursgraduationTotale{\useKV[Rapporteurs]{graduationTotale}}%
  \xdef\RapporteurscentreCroix{\useKV[Rapporteurs]{centreCroix}}%
  \xdef\Rapporteurscorrection{\useKV[Rapporteurs]{correction}}%
  \xdef\Rapporteursconstruction{\useKV[Rapporteurs]{construction}}%
  \xdef\RapporteursafficheRapporteur{\useKV[Rapporteurs]{afficheRapporteur}}%
  \begin{Geometrie}[CoinBG=\RapporteursCoinBG,CoinHD=\RapporteursCoinHD]
    trace feuillet;
    input \persopath/commandes/rapporteurs.mp
    pair A[],B,C[];
    %%%% Paramètres
    color couleur;
    numeric angleEnonce,angleRapporteur;
    numeric decalagexEncart,decalageyEncart,decalagexEnonce,decalageyEnonce;
    boolean graduationInterieure,rapporteurAleph,correction,construction,afficheRapporteur;
    picture enonce,reponse;
    %%%%
    couleur=\Rapporteurscouleur;
    angleRapporteur:=\RapporteursangleRapporteur;
    angleEnonce:=\RapporteursangleEnonce;
    decalagexEncart:=\RapporteursdecalagexEncart;
    decalageyEncart:=\RapporteursdecalageyEncart;
    decalagexEnonce:=\RapporteursdecalagexEnonce;
    decalageyEnonce:=\RapporteursdecalageyEnonce;
    graduationInterieure=\RapporteursgraduationInterieure;
    rapporteurAleph=\RapporteursrapporteurAleph;
    Total:=\RapporteursgraduationTotale;% pour graduer de 10en 10
    CentreCroix:=\RapporteurscentreCroix;%Pour avoir une croix au centre du rapporteur
    correction=\Rapporteurscorrection;
    construction=\Rapporteursconstruction;
    afficheRapporteur=\RapporteursafficheRapporteur;
    %%%% Points utiles     
    A0=u*(3.75,0.5);
    B=u*(7.75,0.5);
    if graduationInterieure:              
       A1=A0 shifted (u,0);
       A2=rotation(A1,A0,angleRapporteur);
    else:
       A1=A0 shifted (-u,0);
       A2=rotation(A1,A0,-angleRapporteur);
    fi;
    %%%% Rapporteur
    enonce=image(
      if construction:
         if not afficheRapporteur and not correction:
            marque_p:="croix";
            pointe(A0);
         fi;
         draw demidroite(A0,A1) withpen pencircle scaled 1.5bp withcolor couleur;
         label.bot(btex $O$ etex,A0);
         if graduationInterieure:
            label.bot(btex $x$ etex,B);
         else:
            label.bot(btex $x$ etex,symetrie(B,A0));
         fi;
         if correction:
            draw demidroite(A0,A2) withpen pencircle scaled 1.5bp withcolor couleur;
            if graduationInterieure:
               draw marqueangle(A1,A0,A2,0);
               fill coloreangle(A1,A0,A2) withcolor couleur;
               label.bot(btex $y$ etex,B) rotatedaround(A0,angleRapporteur);
	         else:
               draw marqueangle(A2,A0,A1,0);
               fill coloreangle(A2,A0,A1) withcolor couleur;
    		      label.bot(btex $y$ etex,symetrie(B,A0)) rotatedaround(A0,-angleRapporteur);
        		fi;
         fi;
      else:
         draw demidroite(A0,A1) withpen pencircle scaled 1.5bp withcolor couleur;
         draw demidroite(A0,A2) withpen pencircle scaled 1.5bp withcolor couleur;
         if (not afficheRapporteur):
            label.bot(btex $O$ etex,A0);
            if graduationInterieure:
               label.bot(btex $x$ etex,B);
               label.bot(btex $y$ etex,B) rotatedaround(A0,angleRapporteur);
            else:
               label.bot(btex $x$ etex,symetrie(B,A0));
               label.bot(btex $y$ etex,symetrie(B,A0)) rotatedaround(A0,-angleRapporteur);
            fi;
         fi;
         if graduationInterieure:
            draw marqueangle(A1,A0,A2,0);
            fill coloreangle(A1,A0,A2) withcolor couleur;
         else:
            draw marqueangle(A2,A0,A1,0);
            fill coloreangle(A2,A0,A1) withcolor couleur;
         fi;
      fi;
      if afficheRapporteur:
         if rapporteurAleph:
            draw rapporteuraleph(A0,B,1);
         else:
            draw myrapporteurdouble(A0,B,1);
         fi;
      else:
         if correction:
            draw myrapporteurdouble(A0,B,1);
         fi;
      fi;
    );
    %%%% Encart réponse
    reponse=image(
      C0=u*(7,2);
      C1=C0 + (2u,0); 
      C2=C1 + (0,1.5u);
      C3=C2 + (-2u,0);
      C4=iso(C2,C3)-(0,0.5u);       
      draw C0--C1--C2--C3--cycle;
      label(btex Mesure etex,C4);
      if construction:
         C5=C4-(0,0.5u);
         label(btex \ang{\RapporteursangleRapporteur} etex,C5);      
      else:
         if correction:
            C5=C4-(0,0.5u);
            label(btex {\red \ang{\RapporteursangleRapporteur}} etex,C5);      
         else:
            for i:=3 upto 17:
               C[5+i]=C3-(0,1.25u)+(i*0.1*u,0);
               label(btex . etex,C[5+i]);
            endfor;%
         fi;
      fi;
    );
    %%%% Tracés
    draw enonce rotatedaround(A0,-angleEnonce) shifted (decalagexEnonce*u,decalageyEnonce*u);
    draw reponse shifted(decalagexEncart*u,decalageyEncart*u);     
 \end{Geometrie}
}

